plugins {
    id 'java-library'
    id 'jacoco'
}

ext {
    junitVersion = '5.3.2'
    jacksonVersion = '2.9.8'
    reactorVersion = '3.2.3.RELEASE'
}

apply from: file('maven.gradle')

group = 'org.mikeneck.pixela'
version = '0.1'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

subprojects {
    group = rootProject.group
    version = rootProject.version
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    api project(':pixela-java-client-api')
    implementation project(':pixela-java-client-default-impl')

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: junitVersion
    testRuntimeClasspath "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.23.4'
    testImplementation group: 'io.projectreactor', name: 'reactor-test', version: reactorVersion
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.11.1'
    testRuntimeClasspath group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
    testImplementation group: 'net.javacrumbs.json-unit', name: 'json-unit-assertj', version: '2.2.0'
}

test {
    useJUnitPlatform()
}

jacoco {
    toolVersion = '0.8.2'
}

task testReport(type: TestReport) {
    reportOn subprojects*.tasks*.withType(Test)
    destinationDir = file("$buildDir/reports/tests/test")
    group = 'verification'
}

task jacocoMerge(type: JacocoMerge) {
    group = 'coverage'

    dependsOn subprojects*.tasks*.withType(Test)

    executionData((tasks as TaskContainer).getByPath(':pixela-java-client-api:test'))
    executionData((tasks as TaskContainer).getByPath(':pixela-java-client-default-impl:test'))
    executionData((tasks as TaskContainer).getByPath(':integration-test:test'))
}

task jacocoReport(type: JacocoReport) {
    group = 'coverage'

    dependsOn 'jacocoMerge'

    executionData tasks.withType(JacocoMerge).findAll { it.name == 'jacocoMerge' }
    additionalSourceDirs files(subprojects.sourceSets.main.allSource.srcDirs)
    additionalClassDirs files(subprojects.sourceSets.main.output)
    reports {
        xml.enabled = true
        xml.destination = file("$buildDir/reports/jacoco/report.xml")
        html.enabled = true
        html.destination = file("$buildDir/jacoco/report-html")
    }
}

task jacoco {
    group = 'coverage'
    dependsOn 'jacocoReport'
}

task qualityCheck {
    group = 'verification'
    dependsOn 'jacoco'
    finalizedBy 'testReport'
}

task localInstall(
        group: 'publish',
        dependsOn: [
                ':pixela-java-client-api:install',
                ':pixela-java-client-default-impl:install'])

if (canPublish()) {
    task publish(
            group: 'publish',
            dependsOn: [
                    ':pixela-java-client-api:uploadArchives',
                    ':pixela-java-client-default-impl:uploadArchives'])
}

task echoHome {
    doLast {
        println System.getProperty('user.home')
    }
}
